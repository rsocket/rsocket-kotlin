/*
 * Copyright 2016 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "com.gradle.build-scan" version "1.12.1"
    id 'com.jfrog.bintray' version '1.7.3' apply false
    id 'com.jfrog.artifactory' version '4.7.1' apply false
    id "org.jetbrains.kotlin.jvm" version "1.2.41" apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    group = 'io.rsocket.kotlin'
    version = '0.9-SNAPSHOT'

    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar, jar
    }

    repositories {
        jcenter()
    }

    dependencies {
        api "io.netty:netty-buffer:4.1.24.Final"
        api "org.reactivestreams:reactive-streams:1.0.2"
        api "com.google.code.findbugs:jsr305:3.0.2"
        api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.41"
        api 'io.reactivex.rxjava2:rxjava:2.1.13'
        api "org.slf4j:slf4j-api:1.7.25"

        testImplementation "junit:junit:4.12"
        testImplementation "org.mockito:mockito-core:2.18.3"
        testImplementation "org.hamcrest:hamcrest-library:1.3"
        testImplementation "org.slf4j:slf4j-log4j12:1.7.25"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {

    subprojects {
        plugins.withId('com.jfrog.bintray') {
            bintray {
                user = project.property('bintrayUser')
                key = project.property('bintrayKey')

                publications = ['maven']
                publish = true

                pkg {
                    repo = 'RSocket'
                    name = 'rsocket-kotlin'
                    licenses = ['Apache-2.0']

                    issueTrackerUrl = 'https://github.com/rsocket/rsocket-kotlin/issues'
                    websiteUrl = 'https://github.com/rsocket/rsocket-kotlin'
                    vcsUrl = 'https://github.com/rsocket/rsocket-kotlin.git'
                    githubRepo = 'rsocket/rsocket-kotlin'
                    githubReleaseNotesFile = 'README.md'

                    version {
                        name = project.version
                        released = new Date()
                        vcsTag = project.version

                        gpg {
                            sign = true
                        }
                    }
                }
            }

            tasks.bintrayUpload.dependsOn tasks.jar, tasks.sourcesJar, tasks.javadocJar
        }

        plugins.withId('com.jfrog.artifactory') {
            artifactory {
                publish {
                    contextUrl = 'https://oss.jfrog.org'

                    repository {
                        repoKey = 'oss-snapshot-local'
                        username = project.property('bintrayUser')
                        password = project.property('bintrayKey')
                    }

                    defaults {
                        publications('maven')
                    }
                }
            }

            artifactoryPublish {
                dependsOn jar
            }
        }
    }
}

description = 'RSocket-kotlin: Reactive Streams over network boundary with Kotlin/Rxjava'

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}
